if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# for refference and future fixing https://github.com/jandamm/zgenom/blob/main/functions/__zgenom_autoupdate
# might break when opening more terminals at the same time, look into locking
# todo spawn another unfocused terminal that handels the update and then closes
function __autoupdate() {
  now=$(command date '+%s')
  local file=$ZDOTDIR/.updated
  if [[ ! -f $file ]]; then
    # We've never run, create the lastupdate-file and set now as interval.
    printf 0 > $file
    last_update=$now
  else
    last_update=$(( $now - $(< $file) - 7 * 86400 ))
  fi
  if [[ $last_update -ge 0 ]]; then
    echo $now > $file
    autoload -Uz $ZDOTDIR/.antidote/functions/antidote
    antidote update
  fi
}

source $ZDOTDIR/config.zsh

# drive config with antidote
ANTIDOTE_HOME=$ZDOTDIR/.zplugins
ANTIDOTE_DIR=$ZDOTDIR/.antidote
zstyle ':antidote:bundle' use-friendly-names 'yes'
zstyle ':antidote:bundle' file $ZDOTDIR/.zplugins.txt

# load antidote
if [[ ! $ZDOTDIR/.zplugins.zsh -nt $ZDOTDIR/.zplugins.txt ]]; then
  [[ -e $ANTIDOTE_DIR ]] \
    || git clone --depth=1 https://github.com/mattmc3/antidote.git $ANTIDOTE_DIR
  (
    source $ANTIDOTE_DIR/antidote.zsh
    antidote bundle <$ZDOTDIR/.zplugins.txt >$ZDOTDIR/.zplugins.zsh
  )
fi

__autoupdate

source $ZDOTDIR/.zplugins.zsh

## Bootstrap interactive sessions
if [[ $TERM != dumb ]]; then
  autoload -Uz compinit && compinit -u -d $ZSH_CACHE/zcompdump
  source $ZDOTDIR/keybinds.zsh
  source $ZDOTDIR/completion.zsh
  source $ZDOTDIR/aliases.zsh
  source $ZDOTDIR/functions.zsh
  autopair-init
fi

# To customize prompt, run `p10k configure` or edit ~/.config/zsh/.p10k.zsh.
[[ ! -f ~/.config/zsh/.p10k.zsh ]] || source ~/.config/zsh/.p10k.zsh
