ANTIDOTE_DIR="${ZDOTDIR:-$HOME}/.antidote"

## Bootstrap antidote
if [[ ! -d "${ZDOTDIR:-$HOME}/.antidote" ]]; then
  echo "Installing https://github.com/mattmc3/antidote"
  git clone --depth=1 https://github.com/mattmc3/antidote.git "$ANTIDOTE_DIR"
fi

zsh_plugins=${ZDOTDIR:-$HOME}/.zsh_plugins

# Ensure the .zsh_plugins.txt file exists so you can add plugins.
[[ -f ${zsh_plugins}.txt ]] || touch ${zsh_plugins}.txt

# Lazy-load antidote from its functions directory.
fpath=("$ANTIDOTE_DIR/functions" $fpath)
autoload -Uz antidote

# Generate a new static file whenever .zsh_plugins.txt is updated.
if [[ ! ${zsh_plugins}.zsh -nt ${zsh_plugins}.txt ]]; then
  antidote bundle <${zsh_plugins}.txt >|${zsh_plugins}.zsh
fi

# Source your static plugins file.
source ${zsh_plugins}.zsh

export SSH_AUTH_SOCK=/Users/kedar/Library/Containers/com.maxgoedjen.Secretive.SecretAgent/Data/socket.ssh

if [[ -d "$ZDOTDIR/conf.d" ]]; then
    for config_file ($ZDOTDIR/conf.d/*.zsh(N)); do
        source "$config_file"
    done
fi

if (( $+commands[bat] )); then
  export MANPAGER="sh -c 'col -bx | bat -l man -p'"
fi

if (( $+commands[nvim] )); then
  alias vim='nvim'
  export EDITOR=nvim
fi

if (( $+commands[fzf] )); then
  source <(fzf --zsh)
fi

export PATH="/opt/homebrew/opt/dotnet@8/bin:$PATH"

eval "$(zoxide init --cmd cd zsh)"

cdmk() { mkdir -p $1 && cd $1 }
backup() { cp "$1"{,.bak}; }
